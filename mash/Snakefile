from collections import defaultdict
import pandas as pd

samp2fastqs = defaultdict(list)
for row in pd.read_table("dieback_only.tsv").itertuples():
    if not pd.isna(row.read1):
        samp2fastqs[row.sample].append(row.read1)
    if not pd.isna(row.read2):
        samp2fastqs[row.sample].append(row.read2)
    if not pd.isna(row.interleaved):
        samp2fastqs[row.sample].append(row.interleaved)

rule mashdist:
    input:
        expand("tmp/{sample}.msh", sample=samp2fastqs)
    output:
        "distances.tsv"
    log:
        "distances.tsv.log"
    conda: "env.yml"
    threads: 48
    shell:
        "mash triangle"
        "   -p {threads}"
        "   -E"
        "   {input}" # needs input twice
        " >{output}"
        " 2>{log}"


rule samplefile:
    input:
        lambda wc: samp2fastqs[wc.sample]
    output:
        temp(pipe("tmp/{sample}.fastq.gz"))
    shell:
        "cat {input} > {output}"

rule mashsketch:
    input:
        "tmp/{sample}.fastq.gz"
    output:
        "tmp/{sample}.msh"
    log:
        "tmp/{sample}.msh.log"
    threads: 1
    conda: "env.yml"
    params:
        ksize=21,
        sketchsize=10000,
    shell:
        "mash sketch"
        "   -k {params.ksize}"
        "   -s {params.sketchsize}"
        "   -r -m 2 -g 600000000"
        "   -p {threads}"
        "   -o {output}"
        "   {input}"
        " >{log} 2>&1"
